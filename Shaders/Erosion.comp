#version 460 core

layout (local_size_x = 16, local_size_y = 16) in;

struct Output {
    vec4 position;
    vec4 normal;
    uint biomeIDs[3];
    float biomeWeight[3];
    float waterAmount;
    float sedimentAmount;
};

layout (std430, binding = 0) buffer Inputs {
    Output inputs[];
};

layout (std430, binding = 1) buffer Outputs {
    int outputs[];
};



uniform int gridSize;
uniform int dropletsPerPos;
uniform float erosionRate = 4.5; // Jak moc se terén eroduje
uniform float depositionRate = 2.2; // Jak moc se sediment usazuje
uniform int numDroplets = 50000; // Počet kapek vody
uniform float inertia = 0.05;
uniform float sedimentCapacityFactor = 4.0;
uniform float minSedimentCapacity = 0.01;
uniform float erodeSpeed = 2.0;
uniform float depositSpeed = 0.5;
uniform float gravity = 9.8;
uniform float initialWaterVolume = 10.0;
uniform float initialSpeed = 2.0;
uniform int dropletIdx;
#define BRUSH_R 3
#define BRUSHPREC (1024 * 16)
uniform int brush_weights[BRUSH_R + 1 + BRUSH_R][BRUSH_R + 1 + BRUSH_R];
uniform float brush[] = {
      0.0f, 0.0f, 0.1f, 0.2f, 0.3f, 0.2f, 0.1f, 0.0f, 0.0f,
      0.0f, 0.2f, 0.4f, 0.6f, 0.7f, 0.6f, 0.4f, 0.2f, 0.0f,
      0.1f, 0.4f, 0.7f, 0.9f, 1.0f, 0.9f, 0.7f, 0.4f, 0.1f,
      0.2f, 0.6f, 0.9f, 1.0f, 1.0f, 1.0f, 0.9f, 0.6f, 0.2f,
      0.3f, 0.7f, 1.0f, 1.0f, 1.0f, 1.0f, 1.0f, 0.7f, 0.3f,
      0.2f, 0.6f, 0.9f, 1.0f, 1.0f, 1.0f, 0.9f, 0.6f, 0.2f,
      0.1f, 0.4f, 0.7f, 0.9f, 1.0f, 0.9f, 0.7f, 0.4f, 0.1f,
      0.0f, 0.2f, 0.4f, 0.6f, 0.7f, 0.6f, 0.4f, 0.2f, 0.0f,
      0.0f, 0.0f, 0.1f, 0.2f, 0.3f, 0.2f, 0.1f, 0.0f, 0.0f,
    };

vec2 hash(vec2 p) {
    vec2 offsetA = vec2(127.1, 311.7);
    vec2 offsetB = vec2(269.5, 183.3) * 2.0;

    return fract(sin(vec2(
        dot(p, offsetA),
        dot(p, offsetB)
    )) * 43758.5453);
}

// Vraci vysku v danem miste s interpolaci
float CalculateHeightAndGradient (vec2 pos, out vec2 gradient) {
    int coordX = int(pos.x);
    int coordY = int(pos.y);

    float x = fract(pos.x);
    float y = fract(pos.y);

    int nodeIndexNW = coordY * gridSize + coordX;
    bool has_next_x = coordX + 1 < gridSize;
    bool has_next_y = coordY + 1 < gridSize;

    float heightNW = inputs[nodeIndexNW].position.y;
    float heightNE = has_next_x ? inputs[nodeIndexNW + 1].position.y : heightNW;
    float heightSW = has_next_y ? inputs[nodeIndexNW + gridSize].position.y : heightNW;
    float heightSE = has_next_x && has_next_y ? inputs[nodeIndexNW + gridSize + 1].position.y : heightNW;

    // Calculate droplet's direction of flow with bilinear interpolation of height difference along the edges
    float gradientX = (heightNE - heightNW) * (1 - y) + (heightSE - heightSW) * y;
    float gradientY = (heightSW - heightNW) * (1 - x) + (heightSE - heightNE) * x;

    // Calculate height with bilinear interpolation of the heights of the nodes of the cell
    float height = heightNW * (1 - x) * (1 - y) + heightNE * x * (1 - y) + heightSW * (1 - x) * y + heightSE * x * y;
    gradient = vec2(gradientX, gradientY);

    return height;
}

void main() {
    uint x = gl_GlobalInvocationID.x;
    uint y = gl_GlobalInvocationID.y;
    if (x >= gridSize || y >= gridSize) return;


    vec2 chunkPos = vec2(x,y);
    vec2 chunkOffset = hash(vec2(x + dropletIdx, y + dropletIdx));
    vec2 dropletPos = chunkPos + chunkOffset;
    
    float speed = initialSpeed;
    float water = initialWaterVolume;

    float sediment = 0.0;  // Kolik sedimentu kapka nese
    vec2 direction = vec2(0.0);

    for (int i = 0; i < 50; i++) {
        if(water <= 0.01 || speed <= 0.01) break;
        vec2 gradient;
        vec2 temp;
        float prevHeight = CalculateHeightAndGradient(dropletPos, gradient);
        vec2 nextDirection = direction * inertia - gradient * (1.0 - inertia);
        if (length(nextDirection) < 0.0001) break;
        //else nextDirection = normalize(nextDirection);
    
        vec2 nextPos = dropletPos + nextDirection * 0.5;
        if (nextPos.x < 0.0 || nextPos.x >= gridSize - 1 || nextPos.y < 0.0 || nextPos.y >= gridSize - 1)
            break;

        float newHeight = CalculateHeightAndGradient(nextPos, temp);
        float deltaHeight = newHeight - prevHeight;

        float capacity = max(-deltaHeight * speed * water * sedimentCapacityFactor, minSedimentCapacity);

    
        if (sediment > capacity || deltaHeight > 0.0) {
            // Spočítáme kolik chceme uložit
            float amountToDeposit = (deltaHeight > 0.0)
                ? min(deltaHeight, sediment)
                : (sediment - capacity) * depositSpeed;

            // Normalizace na BRUSHPREC a přepočet pro celá čísla
            int totalDeposit = int(amountToDeposit * BRUSHPREC * depositionRate);

            if (totalDeposit > 0) {
                ivec2 cell = ivec2(floor(dropletPos));
                vec2 offset = fract(dropletPos);

                uint centerIndex = uint(dropletPos.y) * gridSize + uint(dropletPos.x);
                float centerHeight = inputs[centerIndex].position.y;

                // Výpočet průměru sousedních bodů
                float sum = 0.0;
                float minHeight = centerHeight;
                ivec2 minPos = ivec2(int(x), int(y));
                int samples = 0;

                for (int oy = -1; oy <= 1; oy++) {
                    for (int ox = -1; ox <= 1; ox++) {
                        int nx = int(x) + ox;
                        int ny = int(y) + oy;
                        if (nx >= 0 && nx < gridSize && ny >= 0 && ny < gridSize) {
                            uint ni = uint(ny) * gridSize + uint(nx);
                            float h = inputs[ni].position.y;
                            sum += h;
                            samples++;

                            if (h < minHeight) {
                                minHeight = h;
                                minPos = ivec2(nx, ny);
                            }
                        }
                    }
                }

                float avg = sum / float(samples);
                float threshold = 0.01; // Pokud je bod o víc jak X vyšší než okolí, považuj za spike

                ivec2 finalCell = cell;

                // Pokud je bod moc vysoký, přesuneme uložení sedimentu na sousední nejnižší bod
                if (centerHeight - avg > threshold) {
                    finalCell = minPos;
                }

                // Vypočítej indexy znovu podle finálního místa pro ukládání
                vec2 finalOffset = fract(vec2(finalCell));
                uint indexNW = uint(finalCell.y) * gridSize + uint(finalCell.x);
                uint indexNE = indexNW + 1;
                uint indexSW = indexNW + gridSize;
                uint indexSE = indexSW + 1;

                float wNW = (1.0 - finalOffset.x) * (1.0 - finalOffset.y);
                float wNE = finalOffset.x * (1.0 - finalOffset.y);
                float wSW = (1.0 - finalOffset.x) * finalOffset.y;
                float wSE = finalOffset.x * finalOffset.y;

                // Výpočet částí
                int dNW = int(float(totalDeposit) * wNW);
                int dNE = int(float(totalDeposit) * wNE);
                int dSW = int(float(totalDeposit) * wSW);
                int dSE = totalDeposit - dNW - dNE - dSW;

                atomicAdd(outputs[indexNW], dNW);
                atomicAdd(outputs[indexNE], dNE);
                atomicAdd(outputs[indexSW], dSW);
                atomicAdd(outputs[indexSE], dSE);

                sediment -= float(totalDeposit) / BRUSHPREC;
            }

        } else {
            float slope = length(temp); // velikost gradientu

            float erosionFromCapacity = (capacity - sediment) * erodeSpeed;
            float rawErosion = erosionFromCapacity * slope;

            float amountToErode = min(rawErosion, -deltaHeight);

            if (amountToErode > 0.0) {
                uint idx = uint(dropletPos.y) * gridSize + uint(dropletPos.x);

                float currentHeight = inputs[idx].position.y;
                float maxErode = max(currentHeight - 0.01, 0.0); // Bezpečnostní mez – nenecháme jít do nuly

                float finalErode = min(amountToErode, maxErode);
                int erosionAmount = -int(finalErode * BRUSHPREC * erosionRate);
                atomicAdd(outputs[idx], erosionAmount);

                sediment += finalErode;
            }
        }

        // Update pozice a smeru
        direction = nextDirection;
        dropletPos = nextPos;
        prevHeight = newHeight;
        speed = sqrt(speed * speed + deltaHeight * gravity);
        water *= (1.0 - 0.005);
    }
}